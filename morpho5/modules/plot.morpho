import graphics
import color

/* 3d cross product */
fn cross3d(a, b) {
  return Matrix([ a[1]*b[2]-a[2]*b[1], a[2]*b[0]-a[0]*b[2], a[0]*b[1]-a[1]*b[0] ])
}

/* Bounding box */
fn bbox(m) {
  var col = m.transpose()
  var bnds = []
  for (i in 0...col.dimensions()[1]) bnds.append(bounds(col.column(i)))
  return bnds
}

/** Deduces the plot color for a given grade */
fn plotcolorforgrade(grade, color) {
  if (isnil(color)) return Gray(0.5) // No color provided, so use a default color
  if (islist(color) || isarray(color)) {
      if (color.count()==3 && isnumber(color[0]) && isnumber(color[1]) && isnumber(color[2])) return color // Color was given as a simple list

      // Different colors for each grade
      if (isobject(color[grade]) ||
          islist(color[grade]) ||
          ismatrix(color[grade])) return color[grade]

      if (isnil(color[grade])) return Gray(0.5)
  }
  if (isobject(color)) return color
  print "Color specification for plotmesh should be either a color, a list of [r,g,b] values or a list of colors for each grade."
}

/** Finds the appropriate color for a given element */
fn plotcolorforelement(color, element) {
  var col=color
  if (ismatrix(col)) col=color.column(element)
  return col
}

/** Converts a color to a matrix */
fn plotcolortomatrix(color) {
  if (ismatrix(color)) return color
  if (islist(color)) return Matrix(color)
  if (isobject(color)) return Matrix(color.rgb(0))
  print "Could not convert color to matrix."
  return nil
}

/** Plots elements of a mesh
 * @param[in] mesh - mesh to plot
 * @param[in] selection - selection
 * @param[in] grade - Grade to show
 * @param[in] color - How to color elements:
                      can be a single color
                      a matrix with columns corresponding to elements
                      a list of such matrices for each grade */
fn plotmesh(mesh, selection=nil, grade=nil, color=nil,  filter=nil, transmit=nil) {
  var g = Graphics()
  var glist = []
  if (isnil(grade)) glist.append(mesh.maxgrade())
  if (islist(grade)) for (q in grade) glist.append(q)
  if (isnumber(grade)) glist.append(grade)

  var vert = mesh.vertexmatrix()
  var flat = nil

  var bb = bbox(vert)
  var size[bb.count()]
  for (x, i in bb) {
    size[i]=x[1]-x[0]
    if (abs(size[i])<1e-10) flat = i
  }

  if (glist.ismember(0)) { // Show points as spheres
    var vcol=plotcolorforgrade(0, color) // Will either be a color or a matrix
    var np = mesh.count(0)
    for (i in 0...np) {
      if (isselection(selection) && !selection.isselected(0, i)) continue // Skip unselected components

      g.display(Sphere(vert.column(i), 0.025, color=plotcolorforelement(vcol, i), filter=filter, transmit=transmit))
    }
  }

  if (glist.ismember(1)) { // Show lines as cylinders
    var lcol=plotcolorforgrade(1, color) // Will either be a color or a matrix
    var lines=mesh.connectivitymatrix(0,1)
    if (issparse(lines)) {
      var nl = mesh.count(1)
      for (i in 0...nl) {
        if (isselection(selection) && !selection.isselected(1, i)) continue // Skip unselected components
        var el = lines.rowindices(i)
        g.display(Cylinder(vert.column(el[0]), vert.column(el[1]),
                          aspectratio=0.05, color=plotcolorforelement(lcol, i), filter=filter, transmit=transmit))
      }
    }
  }

  if (glist.ismember(2)) { // Show faces
    var fcol=plotcolorforgrade(2, color) // Will either be a color or a matrix
    var faces=mesh.connectivitymatrix(0,2)
    var interpolate=false

    if (issparse(faces)) {
      var nf = mesh.count(2)
      var nnv = 0, nnt = nf // Count number of triangles and vertices generated
      if (isselection(selection)) nnt=selection.idlistforgrade(2).count()

      if (ismatrix(fcol) &&
          fcol.dimensions()[1]!=nf &&
          fcol.dimensions()[1]==mesh.count(0)) interpolate = true

      var nvert = Matrix(3, 3*nnt) // New array of vertices
      var norm = Matrix(3, 3*nnt) // New array of normals
      var ncol = Matrix(3, 3*nnt) // New array of normals
      var tri = Sparse(3*nnt, nnt) // Connectivity

      nnt=0
      for (i in 0...nf) { // Draw a triangle for each face
        if (isselection(selection) && !selection.isselected(2, i)) continue // Skip unselected components

        var el = faces.rowindices(i)
        var v[3]
        for (p, i in el) v[i]=vert.column(p)
        var normal
        if (flat) {
          normal=Matrix(bb.count())
          normal[flat]=1
        } else {
          normal=cross3d(v[2]-v[1], v[1]-v[0])
          if (normal.inner(v[0])<0) {
            normal=-normal
          }
        }

        var tcol
        if (!interpolate) tcol = plotcolortomatrix(plotcolorforelement(fcol, i))
        for (j in 0...3) {
          tri[nnv,nnt]=1
          nvert.setcolumn(nnv, v[j])
          norm.setcolumn(nnv, normal)
          if (interpolate) tcol = plotcolortomatrix(plotcolorforelement(fcol, el[j]))
          ncol.setcolumn(nnv, tcol)
          nnv+=1
        }
        nnt+=1
      }

      g.display(TriangleComplex(nvert, norm, ncol, tri, filter=filter, transmit=transmit))
    }

  }

  return g
}

/** Visualizes a selection
 * @param[in] mesh - mesh to plot
 * @param[in] selection - selection
 * @param[in] grade - Grades to show */
fn plotselection(mesh, selection, grade=nil, filter=nil, transmit=nil) {
  var ngrades = mesh.maxgrade()
  var col[ngrades+1]

  for (g in 0..ngrades) {
    col[g]=nil
    if (islist(grade) && !grade.ismember(g)) continue // Skip over grades
    else if (isint(grade) && grade!=g) continue

    var selected = selection.idlistforgrade(g)
    if (selected.count()==0) continue                 // Skip over empty grade

    var nel = mesh.count(g)                           // Make a color matrix
    var cmat = Matrix(3, nel)
    for (i in 0...nel) {
      cmat.setcolumn(i, Matrix([0.5,0.5,0.5]))
    }

    for (i in selected) {                             // Highlight selected
      cmat.setcolumn(i, Matrix([1,0,0]))
    }

    col[g]=cmat
  }

  return plotmesh(mesh, grade=grade, color=col, filter=filter, transmit=transmit)
}

/** Visualizes a field
 * @param[in] field - the field
 * @param[in] grade - Grade to show
 * @param[in] colormap - Colormap to use
 * @param[in] style - style to use
 * @param[in] scale - whether or not to scale values */
fn plotfield(field, grade=nil, colormap=nil, style=nil, scale=true, filter=nil, transmit=nil) {
  var mesh = field.mesh()
  var shape = field.shape()

  var ngrades = shape.count()
  var showgrades = [] // Grades to show
  var col[ngrades+1]

  var cm=colormap
  if (cm==nil) cm = HueMap()    // Default color map

  var bnd=bounds(field)         // Find bounds for the field
  var sc = bnd[1]-bnd[0]
  if (abs(sc)>1e-16) sc=1/sc else sc = 1

  if (style=="interpolate") { // Interpolate scalar field onto areas
    var nv=mesh.count(0)       // Number of elements in this grade
    var cmat = Matrix(3, nv)   // Make a color matrix

    for (i in 0...nv) {
      var val = field[0, i]
      if (scale) val=(val-bnd[0])*sc
      cmat.setcolumn(i, Matrix(cm.rgb(val)))
    }

    col[2]=cmat
    showgrades.append(2)
  } else {
    for (g in 0...ngrades) {
      if (shape[g]==0) continue

      var nel=mesh.count(g)       // Number of elements in this grade
      var cmat = Matrix(3, nel)   // Make a color matrix

      for (i in 0...nel) {
        var val = field[g, i]
        if (scale) val=(val-bnd[0])*sc
        cmat.setcolumn(i, Matrix(cm.rgb(val)))
      }

      col[g]=cmat
      showgrades.append(g)
    }
  }

  return plotmesh(mesh, grade=showgrades, color=col, filter=filter, transmit=transmit)
}
