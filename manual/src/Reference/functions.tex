\hypertarget{functions}{%
\section{Functions}\label{functions}}

A function in morpho is defined with the \texttt{fn} keyword, followed
by the function's name, a list of parameters enclosed in parentheses,
and the body of the function in curly braces. This example computes the
square of a number:

\begin{lstlisting}
fn sqr(x) {
  return x*x
}
\end{lstlisting}

Once a function has been defined you can evaluate it like any other
morpho function.

\begin{lstlisting}
print sqr(2)
\end{lstlisting}

\hypertarget{variadic}{%
\subsection{Variadic}\label{variadic}}

As well as regular parameters, functions can also be defined with
\emph{variadic} parameters:

\begin{lstlisting}
fn func(x, ...v) {
    for (a in v) print a
}
\end{lstlisting}

This function can then be called with 1 or more arguments:

\begin{lstlisting}
func(1)
func(1, 2)
func(1, 2, 3) // All valid! 
\end{lstlisting}

The variadic parameter \texttt{v} captures all the extra arguments
supplied. Functions cannot be defined with more than one variadic
parameter.

You can mix regular, variadic and optional parameters. Variadic
parameters come before optional parameters:

\begin{lstlisting}
fn func(x, ...v, optional=true) {
    // 
}
\end{lstlisting}

\hypertarget{optional}{%
\subsection{Optional}\label{optional}}

Functions can also be defined with \emph{optional} parameters:

\begin{lstlisting}
fn func(a=1) {
    print a 
}
\end{lstlisting}

Each optional parameter must be defined with a default value (here
\texttt{1}). The function can then be called either with or without the
optional parameter:

\begin{lstlisting}
func()    // a == 1 due to default value
func(a=2) // a == 2 supplied by the user
\end{lstlisting}

\hypertarget{return}{%
\subsection{Return}\label{return}}

The \texttt{return} keyword is used to exit from a function, optionally
passing a given value back to the caller. \texttt{return} can be used
anywhere within a function. The below example calculates the \texttt{n}
th Fibonacci number,

\begin{lstlisting}
fn fib(n) {
  if (n<2) return n
  return fib(n-1) + fib(n-2)
}
\end{lstlisting}

by returning early if \texttt{n\textless{}2}, otherwise returning the
result by recursively calling itself.

\hypertarget{closures}{%
\section{Closures}\label{closures}}

Functions in morpho can form \emph{closures}, i.e.~they can enclose
information from their local context. In this example,

\begin{lstlisting}
fn foo(a) {
    fn g() { return a } 
    return g
}
\end{lstlisting}

the function \texttt{foo} returns a function that captures the value of
\texttt{a}. If we now try calling \texttt{foo} and then calling the
returned functions,

\begin{lstlisting}
var p=foo(1), q=foo(2) 
print p() // expect: 1 
print q() // expect: 2
\end{lstlisting}

we can see that \texttt{p} and \texttt{q} seem to contain different
copies of \texttt{g} that encapsulate the value that \texttt{foo} was
called with.

Morpho hints that a returned function is actually a closure by
displaying it with double brackets:

\begin{lstlisting}
print foo(1) // expect: <<fn g>> 
\end{lstlisting}
